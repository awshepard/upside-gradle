buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.netflix.nebula:nebula-publishing-plugin:5.1.1'
        classpath 'com.netflix.nebula:nebula-release-plugin:6.0.0'
    }
}

apply plugin: "idea"

allprojects {
    def maven_user = System.getenv('UPSIDE_MAVEN_USER')
    def maven_user_api_key = System.getenv('UPSIDE_MAVEN_API_KEY')

    if (maven_user == null) {
        print("UPSIDE_MAVEN_USER is null")
    }

    apply plugin: nebula.plugin.publishing.publications.JavadocJarPlugin
    apply plugin: nebula.plugin.publishing.publications.SourceJarPlugin
    apply plugin: nebula.plugin.release.ReleasePlugin
    apply plugin: nebula.plugin.publishing.maven.MavenPublishPlugin

    repositories {
        maven {
            credentials {
                username = maven_user
                password = maven_user_api_key
            }
            url 'https://upside.jfrog.io/upside/repo/'
        }
        mavenLocal()
    }
    
    publishing {
        repositories {
            maven {
                credentials {
                    username = maven_user
                    password = maven_user_api_key
                }
                if(project.version.toString().contains('SNAPSHOT')) {
                    url 'https://upside.jfrog.io/upside/libs-snapshot-local/'
                } else {
                    url 'https://upside.jfrog.io/upside/libs-release-local/'
                }
            }
        }
    }

    idea {
        module {
            downloadJavadoc = true // defaults to false
            downloadSources = true
        }
    }
}

